apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'org.jetbrains.dokka-android'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

// Working: https://github.com/ajalt/timberkt/blob/master/timberkt/build.gradle
// Working: https://github.com/Faltenreich/TextInputView/blob/develop/textinputview/deploy.gradle

ext {
    libVersion = '0.2.8'
    libGroup = 'com.otaliastudios'
    libName = 'Elements'
    libDescription = 'A modular approach to RecyclerView adapters with reusable, testable, independent, coordinated components.'
    libArtifactId = 'elements'
    githubUrl = "https://github.com/natario1/${libName}"
    githubGit = "https://github.com/natario1/${libName}.git"
}

version = libVersion
group = libGroup
archivesBaseName = libArtifactId

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    // buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName libVersion
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }

    dataBinding.enabled = true
    // lintOptions.abortOnError = false
}

dependencies {
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    api "androidx.recyclerview:recyclerview:$recyclerVersion"
    api "androidx.lifecycle:lifecycle-livedata:$lifecycleVersion"
    api "com.jakewharton.timber:timber:$timberVersion"
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task dokkaJar(type: Jar, dependsOn: dokka) {
    classifier = 'javadoc'
    from dokka.outputDirectory
}

artifacts {
    archives dokkaJar
    archives sourcesJar
}

install.repositories.mavenInstaller.pom.project {
    name libName
    description libDescription
    url githubUrl
    groupId libGroup
    artifactId libArtifactId
    version libVersion
    packaging 'aar'
    scm {
        connection githubGit
        url githubUrl
    }
    licenses {
        license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
        }
    }
    developers {
        developer {
            id 'natario'
            name 'Mattia Iavarone'
        }
    }
}


Properties props = new Properties()
props.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = props.getProperty('bintray.user')
    key = props.get('bintray.key')
    configurations = ['archives']
    override = true
    publish = true
    pkg {
        repo = 'android'
        name = libName
        licenses = ['Apache-2.0']
        vcsUrl = githubGit
        desc = libDescription
        version {
            name = libVersion
            released = new Date()
            vcsTag = libVersion
        }
    }
}



